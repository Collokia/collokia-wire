task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

if (rootProject != project) {
    configure(rootProject) {
        apply plugin: 'idea'
    }
}

buildscript {
    repositories {
        maven {
            url "https://collokia.artifactoryonline.com/collokia/collokia-internal"
            credentials {
                username "${collokiaRepoUser}"
                password "${collokiaRepoPass}"
            }
        }
        maven {
            url 'https://collokia.artifactoryonline.com/collokia/collokia-oss'
        }
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'http://repo.spring.io/plugins-release'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
        classpath "org.springframework.build.gradle:propdeps-plugin:$propdepsVersion"
        classpath "io.vertx:vertx-core:$vertxVersion"
        classpath "io.vertx:vertx-platform:$vertxVersion"
        classpath "io.vertx:vertx-hazelcast:$vertxVersion"
        classpath "com.prezi.gradle.pride:gradle-pride-plugin:$prideVersion"
    }
}

subprojects {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'kotlin'
    apply plugin: 'pride'

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_6

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    javadocJar.dependsOn javadoc

    build.dependsOn sourceJar, javadocJar

    artifacts {
        archives sourceJar, javadocJar
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}"
    }

    dynamicDependencies {
        compile group: 'com.collokia', name: 'collokia-common', version: "${collokiaCommonVersion}"
    }

    repositories {
        maven {
            url "https://collokia.artifactoryonline.com/collokia/collokia-internal"
            credentials {
                username "${collokiaRepoUser}"
                password "${collokiaRepoPass}"
            }
        }
        maven {
            url 'https://collokia.artifactoryonline.com/collokia/collokia-oss'
        }
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
        maven {
            url 'http://repo.spring.io/plugins-release'
        }
    }

    configurations.all {
        resolutionStrategy {
            cacheDynamicVersionsFor 1, 'days'  // things with wildcard version numbers
            cacheChangingModulesFor 0, 'seconds'  // things with same version, but content of module changes, i.e. SNAPSHOT
        }
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
        repositories {
            maven {
                url "https://collokia.artifactoryonline.com/collokia/collokia-internal"
                credentials {
                    username "${collokiaRepoUser}"
                    password "${collokiaRepoPass}"
                }
            }
        }
    }

}